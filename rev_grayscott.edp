// =========================================================================================
// Gray Scott Model
// =========================================================================================
load "iovtk"; // for saving data in paraview format
verbosity=0;
real wdate=1;
string filename="energyData.m"; // for saving datat in matlab format
string foldername = "data_rev"; //for saving ParaView Data
exec("mkdir "+foldername);

real dt=1.0; //time step size
int nTimestep = 10000; //number of iterations
real iterguarda=10; // iterations between each save for paraview
real iterdib=1; // in which iteration start saving for paraview

//-----------------------------------------------------------------------------------------
// Set Parameters
//-----------------------------------------------------------------------------------------
real time=0.0;
real gammau=1.0e-4, gammav=0.5e-4; // diffusion coefficients
real gammaq=1.0e-4, gammap=0.5e-4; // diffusion coefficients
// real feed = 0.05, kill=0.06; //reaction coefficients
real k0plus = 0.01, k0minus = 0.04; //k0minus = feed rate
real k1plus = 1.00, k1minus = 0.01; //k1plus = reaction rate
real k2plus = 0.06, k2minus = 0.01; //k2plus = kill

real x0 = -2.5;
real x1 = 2.5;
real y0 = -2.5;
real y1 = 2.5;
int meshsize = 100;
mesh Th = square(meshsize,meshsize,[x0+(x1-x0)*x, y0+(y1-y0)*y]); // structured mesh
// plot(Th, wait=true); //plot the mesh

//-----------------------------------------------------------------------------------------
// Set Finite Element Spaces
//-----------------------------------------------------------------------------------------
fespace Xh(Th,P1,periodic=[[1,x],[3,x],[2,y],[4,y]]); //define a Finite Element space
//P1 = Piecewise Linear Finite Elements
//periodic boundary conditions
Xh u,u0,ub,uh;
Xh v,v0,vb,vh;
Xh p,p0,pb,ph;
Xh q,q0,qb,qh;


//-----------------------------------------------------------------------------------------
// Define Weak Formulation
//-----------------------------------------------------------------------------------------
problem GSu ( [u],[uh])=
	int2d(Th) ( //integral containing all u^n+1 terms
			(1/dt)*u*uh
			+ gammau*(dx(u)*dx(uh) + dy(u)*dy(uh))
			)
	+int2d(Th) ( //integral containing all other terms
			- (1/dt)*u0*uh
			// + gammau*(dx(u0)*dx(uh) + dy(u0)*dy(uh))
			+ k1plus*u0*v0*v0*uh
			- k1minus*v0*v0*v0*uh
            + k0plus*u0*uh
            - k0minus*q0*uh
			)
// +on(1,2,3,4, u = ub)
;

problem GSv ( [v],[vh])=
	int2d(Th) ( //integral containing all v^n+1 terms
			(1/dt)*v*vh
			+ gammav*(dx(v)*dx(vh) + dy(v)*dy(vh))
			)
	+int2d(Th) ( //integral containing all other terms
			- (1/dt)*v0*vh
			// + gammav*(dx(v0)*dx(vh) + dy(v0)*dy(vh))
			- k1plus*u0*v0*v0*vh
			+ k1minus*v0*v0*v0*vh
            + k2plus*v0*vh
            - k2minus*p0*vh
			)
// +on(1,2,3,4, v = vb)
;

problem GSp ( [p],[ph])=
	int2d(Th) ( //integral containing all v^n+1 terms
			(1/dt)*p*ph
			+ gammap*(dx(p)*dx(ph) + dy(p)*dy(ph))
			)
	+int2d(Th) ( //integral containing all other terms
			- (1/dt)*p0*ph
			// + gammav*(dx(v0)*dx(vh) + dy(v0)*dy(vh))
            - k2plus*v0*ph
            + k2minus*p0*ph
			)
// +on(1,2,3,4, v = vb)
;

problem GSq ( [q],[qh])=
	int2d(Th) ( //integral containing all v^n+1 terms
			(1/dt)*q*qh
			+ gammaq*(dx(q)*dx(qh) + dy(q)*dy(qh))
			)
	+int2d(Th) ( //integral containing all other terms
			- (1/dt)*q0*qh
			// + gammav*(dx(v0)*dx(vh) + dy(v0)*dy(vh))
            - k0plus*u0*qh
            + k0minus*q0*qh
			)
// +on(1,2,3,4, v = vb)
;

//-----------------------------------------------------------------------------------------
// Set the Initial Condition
//-----------------------------------------------------------------------------------------
func real f(real xx, real yy){ //Creates a circle with value 1 in center. 0 outside
	real width = 0.1;
	if (xx < width && xx > -width && yy < width && yy > -width){
		return 1.0;
	}
	else {
		return 0.0;
	}
}
v0 = f(x,y); //initial v uses f
u0 = 1.0; //inital u is 1 everwhere
p0 = 0.0;
q0 = 0.0;
Xh d = u0/(u0 + v0); // concentration of u

//-----------------------------------------------------------------------------------------
//SAVE INITIAL DATA
//-----------------------------------------------------------------------------------------

savevtk(foldername+"/"+"Data_Paraview_0.vtu",Th,u,v,p,q,d,dataname="u v p q d"); // saving data for ParaView


//-----------------------------------------------------------------------------------------
// Run the Main Routine
//-----------------------------------------------------------------------------------------
for (int i=1;i<=nTimestep;i++){

time = time + dt;//update time

GSu; //solve the problem for u
GSv; //solve the problem for v
GSp; //solve the problem for p
GSq; //solve the problem for q
d = u/(u + v); //update concentration
// u = max(min(u,1.0),0.0); //clamp u,v between 0 and 1
// v = max(min(v,1.0),0.0);
plot(d, fill=true, value=true); //plot d

// update for next iteration
u0 = u;
v0 = v;
p0 = p;
q0 = q;

cout << "time = " << time <<  endl;

if (abs(iterdib-i)<1e-5) {
savevtk(foldername+"/"+"Data_Paraview_"+i+".vtu",Th,u,v,p,q,d,dataname="u v p q d"); //saving data for ParaView
iterdib=iterdib+iterguarda;
}

}
//-----------------------------------------------------------------------------------------
// END OF the Main Routine
//-----------------------------------------------------------------------------------------


//-----------------------------------------------------------------------------------------
// SAVING FINAL DATA
//-----------------------------------------------------------------------------------------
{
ofstream gnu("u" + nTimestep + ".gp");
gnu <<u[]<<endl;
}
{
ofstream gnu("v" + nTimestep + ".gp");
gnu <<v[]<<endl;
}
{
ofstream gnu("p" + nTimestep + ".gp");
gnu <<p[]<<endl;
}
{
ofstream gnu("q" + nTimestep + ".gp");
gnu <<q[]<<endl;
}